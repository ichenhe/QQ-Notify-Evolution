apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

int vCode = 20000 + getVersionCode() // The last code is 20001, make sure the result is greater than it.
String vName = getVersionName()
logger.lifecycle("App Version: $vName ($vCode)")

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "cc.chenhe.qqnotifyevo"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode vCode
        versionName vName
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "channel"
    productFlavors {
        normal {
            dimension 'channel'
            buildConfigField 'Boolean', 'PLAY', 'false'
        }
        play {
            dimension 'channel'
            buildConfigField 'Boolean', 'PLAY', 'true'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = 1.8
    }
}

dependencies {
    def lifecycle_version = "2.3.1"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation 'com.oasisfeng.nevo:sdk:2.0.0-rc01'
    implementation "com.jakewharton.timber:timber:5.0.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.amshove.kluent:kluent-android:1.68'
}


private int getVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    try {
        return cmd.execute().text.trim().toInteger()
    } catch (Exception e) {
        logger.error("Failed to get version code with git, return 1 by default.\n${e.message}")
        return 1
    }
}

private String getVersionName() {
    def cmd = 'git describe --tags --long --first-parent --abbrev=7 --dirty=_dev'
    try {
        def v = cmd.execute().text.trim()
        def matcher = v =~ /^v([\d|.]+)-\d+-g[A-Za-z0-9]{7}(_dev)?$/

        if (!matcher) {
            logger.error("Failed to get version name with git.\n" +
                    "Cannot match git tag describe, return <UNKNOWN> by default. raw=$v")
            return "UNKNOWN"
        }
        def suffix = matcher[0][2] ? matcher[0][2] : ''
        return "${matcher[0][1]}$suffix"
    } catch (Exception e) {
        logger.error("Failed to get version name with git, return <UNKNOWN> by default.\n${e.message}")
        return "UNKNOWN"
    }
}
